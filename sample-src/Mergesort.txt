data List(a) of Nil() | Cons(a, List(a));
data And(a, b) of And(a, b);

dealHelper(tf, ls, rs, xs) = case xs
  { Nil() => And(ls, rs)
  | Cons(y, ys) => case tf
    { True() => dealHelper(False(), Cons(y, ls), rs, ys)
    | False() => dealHelper(True(), ls, Cons(y, rs), ys)
    }
  };

deal(xs) = dealHelper(True(), [], [], xs);

merge(xs, ys) = case xs
  { Nil() => ys
  | Cons(a, as) => case ys
    { Nil() => xs
    | Cons(b, bs) => case a <= b
      { True() => Cons(a, merge(as, ys))
      | False() => Cons(b, merge(xs, bs))
      }
    }
  };

isSmall(xs) = case xs
  { Nil() => True()
  | Cons(y, ys) => case ys
    { Nil() => True()
    | Cons(z, zs) => False()
    }
  };

mergesort(xs) = case isSmall(xs) 
  { True() => xs
  | False() => case deal(xs)
    { And(ls, rs) => merge( mergesort(ls) , mergesort(rs) ) }
  };

main() = mergesort([13, 5, 2, 10, 3]);
